buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.freefair.lombok' version '6.3.0'
    id 'kr.entree.spigradle' version '2.4.2'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 17
targetCompatibility = 17

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    version = project.property("version")
    load = STARTUP
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

javadoc {
    options.encoding = 'UTF-8'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/public/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url = 'https://repo.unnamed.team/repository/unnamed-public/' }
}

dependencies {
    // using spigot-api
    compileOnly "org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT"
    // or using paper-api
    // implementation "io.papermc.paper:paper-api:${mcVersion}-R0.1-SNAPSHOT"

    // Add your dependencies here
    // Here are some opinionated dependencies that might help you with your plugin development:

    // Annotation Command Framework: https://github.com/aikar/commands
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    implementation ('net.silthus.configlib:configlib-bukkit:2.6.0') {
        exclude group: 'net.md-5', module: 'bungeecord-chat'
        exclude group: 'org.spigotmc', module: 'spigot-api'
    }

    implementation 'me.yushust.message:core:7.0.0'
    implementation 'me.yushust.message:sourcetype-bukkit-yml:7.0.0'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.17:2.22.2'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

shadowJar {
    classifier ""
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'de.exlll.configlib', "${packageName}.configlib"
    relocate 'me.yushust.message', "${packageName}.nmessage"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    ignoreFailures = false
}

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
